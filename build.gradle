import java.text.SimpleDateFormat

plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version '7.0.0'
}

apply plugin: 'maven-publish'

group "io.th0rgal"
def pluginVersion = "1.95.0"
ext.jitpackGroup = group + ".oraxen"
ext.jitpackVersion = 'main-' + pluginVersion
version = pluginVersion

repositories {
    mavenCentral()
    mavenLocal()
    // Spigot
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    // Because Spigot depends on Bungeecord ChatComponent-API
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    // JitPack
    maven { url 'https://jitpack.io' }
    // ProtocolLib
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    // Minecraft repo (commodore)
    maven { url "https://libraries.minecraft.net/" }
    // PlaceHolderAPI
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // EffectLib
    maven { url 'https://maven.elmakers.com/repository/' }
    // sk89q-repo
    maven { url 'https://maven.enginehub.org/repo/' }
    // CodeMc (bstats)
    maven { url "https://repo.codemc.org/repository/maven-public" }
    // CustomBlockData
    maven { url "https://repo.jeff-media.de/maven2" }
}

compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    // The Spigot API with no shadowing. Requires the OSS repo.
    compileOnly group: "org.spigotmc", name: "spigot", version: "1.16.5-R0.1-SNAPSHOT"
    compileOnly group: "org.spigotmc", name: "spigot-api", version: "1.16.5-R0.1-SNAPSHOT"
    compileOnly group: "com.github.dmulloy2", name: "ProtocolLib", version: "4.7.0"
    compileOnly group: "com.github.Hazebyte", name: "CrateReloadedAPI", version: "5318abfccc"
    compileOnly group: "com.sk89q.worldguard", name: "worldguard-bukkit", version: "7.0.5"
    compileOnly group: "com.github.jojodmo", name: "ItemBridge", version: "-SNAPSHOT"
    compileOnly fileTree(dir: "libs/compile", include: ["*.jar"])
    implementation fileTree(dir: "libs/implement", include: ["*.jar"])
    implementation "org.bstats:bstats-bukkit:2.2.1"
    implementation group: 'com.github.rockswang', name: 'java-curl', version: '1.2.2.190107'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.28'
    implementation "net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"
    implementation "net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT"
    implementation 'dev.jorel.CommandAPI:commandapi-shade:6.2.0'
    implementation 'de.jeff_media:CustomBlockData:1.0-SNAPSHOT'
}

processResources {
    filesNotMatching(["**/*.png", "**/models/**", "**/textures/**"]) {
        expand projectVersion: pluginVersion
    }
}

shadowJar {
    relocate "com.syntaxphoenix.syntaxapi", "io.th0rgal.oraxen.shaded.syntaxapi"
    relocate "org.yaml.snakeyaml", "io.th0rgal.oraxen.shaded.snakeyaml"
    relocate "org.bstats", "io.th0rgal.guardian.shaded.bstats"
    relocate "net.kyori", "io.th0rgal.oraxen.shaded.kyori"
    relocate "de.jeff_media.customblockdata", "io.th0rgal.oraxen.shaded.customblockdata"
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Version': pluginVersion,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

compileJava.dependsOn clean
build.dependsOn shadowJar

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/oraxen/Oraxen"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        grp(MavenPublication) {
            from(components.java)
        }
    }
}