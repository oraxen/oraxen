import java.text.SimpleDateFormat

plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version '7.0.0'
}

apply plugin: 'maven-publish'

group "io.th0rgal"
def pluginVersion = "1.108.1"
ext.jitpackGroup = group + ".oraxen"
ext.jitpackVersion = 'main-' + pluginVersion
version = pluginVersion

repositories {
    mavenCentral()
    // Spigot
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    // Because Spigot depends on Bungeecord ChatComponent-API
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    // JitPack
    maven { url 'https://jitpack.io' }
    // ProtocolLib
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    // Minecraft repo (commodore)
    maven { url "https://libraries.minecraft.net/" }
    // PlaceHolderAPI
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // EffectLib
    maven { url 'https://maven.elmakers.com/repository/' }
    // CodeMc (bstats)
    maven { url "https://repo.codemc.org/repository/maven-public" }
    // CustomBlockData
    maven { url "https://repo.jeff-media.de/maven2" }
    // Placeholder API
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly group: "org.spigotmc", name: "spigot-api", version: "1.17.1-R0.1-SNAPSHOT"
    compileOnly group: "com.github.dmulloy2", name: "ProtocolLib", version: "4.7.0"
    compileOnly group: "com.github.Hazebyte", name: "CrateReloadedAPI", version: "5318abfccc"
    compileOnly group: "com.github.jojodmo", name: "ItemBridge", version: "-SNAPSHOT"
    compileOnly group: "me.clip", name: "placeholderapi", version: "2.10.10"
    compileOnly group: "io.github.qveshn", name: "LightAPI-fork", version: "3.5.2-SNAPSHOT"
    compileOnly fileTree(dir: "libs/compile", include: ["*.jar"])
    implementation fileTree(dir: "libs/implement", include: ["*.jar"])
    implementation group: "org.bstats", name: "bstats-bukkit", version: "2.2.1"
    implementation group: "com.github.oraxen", name: "protectionlib", version: "master-SNAPSHOT"
    implementation group: "com.github.rockswang", name: "java-curl", version: "1.2.2.2"
    implementation group: "org.yaml", name: "snakeyaml", version: "1.28"
    implementation group: "net.kyori", name: "adventure-text-minimessage", version: "4.1.0-SNAPSHOT"
    implementation group: "net.kyori", name: "adventure-platform-bukkit", version: "4.0.0-SNAPSHOT"
    implementation group: "com.github.stefvanschie.inventoryframework", name: "IF", version: "0.10.0"
    implementation group: "dev.jorel.CommandAPI", name: "commandapi-shade", version: "6.3.0"
    implementation group: "de.jeff_media", name: "CustomBlockData", version: "1.0-SNAPSHOT"
}

processResources {
    filesNotMatching(["**/*.png", "**/models/**", "**/textures/**"]) {
        expand projectVersion: pluginVersion
    }
}

shadowJar {
    relocate "com.syntaxphoenix.syntaxapi", "io.th0rgal.oraxen.shaded.syntaxapi"
    relocate "org.yaml.snakeyaml", "io.th0rgal.oraxen.shaded.snakeyaml"
    relocate "org.bstats", "io.th0rgal.oraxen.shaded.bstats"
    relocate "net.kyori", "io.th0rgal.oraxen.shaded.kyori"
    relocate "de.jeff_media.customblockdata", "io.th0rgal.oraxen.shaded.customblockdata"
    relocate "com.github.stefvanschie.inventoryframework", "io.th0rgal.oraxen.shaded.if"
    relocate "com.roxstudio.utils", "io.th0rgal.oraxen.shaded.curl"
    relocate "dev.jorel.commandapi", "io.th0rgal.oraxen.shaded.commandapi"
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Version': pluginVersion,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} " +
                        "(${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} " +
                        "${System.properties['os.version']}"
        )
    }
}

compileJava.dependsOn clean
build.dependsOn shadowJar

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifact shadowJar
        }
    }
}